buildscript {
    ext {
        springBootVersion = '2.4.3'
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
            allowInsecureProtocol = true
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

group 'com.wjx.osiris'
version '1.0.0'


idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

//指定java版本
sourceCompatibility = 1.8
targetCompatibility = 1.8

//指定编码格式
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    maven {
        url 'http://maven.aliyun.com/nexus/content/groups/public/'
        allowInsecureProtocol = true
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    implementation group: 'mysql', name: 'mysql-connector-java'
    compileOnly group: 'org.projectlombok', name: 'lombok'
    annotationProcessor group: 'org.projectlombok', name: 'lombok'
    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.75'
    implementation group: 'org.apache.commons', name: 'commons-lang3'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'ch.qos.logback', name: 'logback-classic'
    implementation group: 'org.springframework', name: 'spring-core'
    implementation group: 'org.springframework', name: 'spring-aspects'
    implementation group: 'org.springframework', name: 'spring-aop'
    implementation group: 'org.springframework', name: 'spring-beans'
    implementation group: 'org.springframework', name: 'spring-context'
    implementation group: 'org.springframework', name: 'spring-webmvc'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'cn.hutool', name: 'hutool-all', version: '5.6.0'
    implementation group: 'com.jcraft', name: 'jsch', version: '0.1.55'
    implementation group: 'javax.servlet', name: 'javax.servlet-api'
}

java {
    withSourcesJar()
    withJavadocJar()
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

task 'create-dirs' {
    doLast {
        sourceSets*.java.srcDirs*.each {
            it.mkdirs()
        }
        sourceSets*.resources.srcDirs*.each {
            it.mkdirs()
        }
    }
}
