buildscript {
    ext {
        springBootVersion = '2.3.2.RELEASE'
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
            allowInsecureProtocol = true
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

group 'com.wjx.osiris'
version '1.0.0'
description 'Java代码'

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

//指定java版本
sourceCompatibility = 1.8
targetCompatibility = 1.8

//指定编码格式
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

repositories {
    mavenLocal()
    maven {
        url 'http://maven.aliyun.com/nexus/content/groups/public/'
        allowInsecureProtocol = true
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api group: 'com.wjx', name: 'qilin', version: '1.0.0'
    annotationProcessor group: 'org.projectlombok', name: 'lombok'
    implementation group: 'cglib', name: 'cglib', version: '3.3.0'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

task 'create-dirs' {
    doLast {
        sourceSets*.java.srcDirs*.each {
            it.mkdirs()
        }
        sourceSets*.resources.srcDirs*.each {
            it.mkdirs()
        }
    }
}